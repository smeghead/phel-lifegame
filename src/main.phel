(ns lifegame\main
  (:require phel\str :as s)
  (:require phel-cli-gui\terminal-gui :as gui)
  (:require lifegame\modules\board :as b)
  (:require lifegame\modules\cell :as c)
  (:require lifegame\modules\tick :as t))

(def *height* 20)
(def *width* 40)

(def char-map {1 "#" 0 " "})

(defn- get-matrix-line [line]
  (apply str (map char-map line)))

(defn- get-matrix-data [matrix]
  (s/join "\n" (map get-matrix-line matrix)))

(defn draw-board [board tick-count]
  (let [matrix (board :matrix)
        width (count (matrix 0))
        height (count matrix)]
    (gui/render 0 0 (get-matrix-data matrix))
    (gui/render 1 (+ *height* 2) (format "tick: %d" tick-count))))

(defn random-board []
  (b/create *height* *width*))

(php/pcntl_signal \SIGINT (fn [] (php/exit)))

(when-not *build-mode*
  (gui/clear-screen)
  (loop [board (random-board)
         tick-count 1]
    (php/usleep 10000)
    (draw-board board tick-count)
    (recur (t/tick board) (inc tick-count))))
